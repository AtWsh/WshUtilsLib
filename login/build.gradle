if (isRunAlone.toBoolean()) {
    apply plugin: 'com.android.application'
}else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion versions.compileSdk
    resourcePrefix "login_"

    defaultConfig {
        if (isRunAlone.toBoolean()) {
            applicationId "it.wsh.cn.login"
        }
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode verCode
        versionName verName

        testInstrumentationRunner deps.support.test.instrumentationRunner

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]
            }
        }

    }

    sourceSets {
        main {
            // 单独调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (isRunAlone.toBoolean()) {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility versions.javaVersion
        targetCompatibility versions.javaVersion
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation deps.support.appcompatv7
    implementation deps.support.constraintLayout
    implementation project(':base')
    annotationProcessor deps.arouter_compiler
}
