// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {
        versions = [
                'javaVersion'           : JavaVersion.VERSION_1_8, // JDK版本

                'minSdk'                : 22, // 最低版本
                'compileSdk'            : 27, // 编译版本
                'targetSdk'             : 27, // 目标版本

                'androidPlugin'         : '3.1.1', // Android Gradle版本
                'buildTools'            : '27.0.3', // 编译工具版本
                'supportLibrary'        : '27.1.1', // Android Support包版本

                'majorNumber'           : 2, // 大版本号
                'minorNumber'           : 3, // 小版本号
                'revisionNumber'        : 1,
                'revisionNumberCMD'     : 'git rev-list HEAD --count', // 提交次数命令
                'revisionDescriptionCMD': 'git describe --always', // 提交记录命令
                'revisionCommitSHACMD'  : 'git rev-parse --short HEAD'
        ]

        deps = [
                test                           : [
                        'junit': "junit:junit:4.12"
                ],
                android                        : [
                        'gradlePlugin'  : "com.android.tools.build:gradle:${versions.androidPlugin}",
                        'greendaoPlugin': 'org.greenrobot:greendao-gradle-plugin:3.2.2'
                ],
                'support'                      : [
                        'test'            : [
                                'runner'               : 'com.android.support.test:runner:1.0.1',
                                'espresso'             : "com.android.support.test.espresso:espresso-core:3.0.1",
                                'instrumentationRunner': "android.support.test.runner.AndroidJUnitRunner"
                        ],
                        'appcompat'       : "com.android.support:support-compat:${versions.supportLibrary}",
                        'appcompatv7'    : "com.android.support:appcompat-v7:${versions.supportLibrary}",
                        'recyclerview'    : "com.android.support:recyclerview-v7:${versions.supportLibrary}",
                        'design'          : "com.android.support:design:${versions.supportLibrary}",
                        'annotations'     : "com.android.support:support-annotations:${versions.supportLibrary}",
                        'multidex'        : "com.android.support:multidex:1.0.2",
                        'constraintLayout': "com.android.support.constraint:constraint-layout:1.1.2"
                ],
                'retrofit'                     : 'com.squareup.retrofit2:retrofit:2.3.0',
                'retrofitConverterGson'        : 'com.squareup.retrofit2:converter-gson:2.3.0',
                'retrofitAdapterRxjava'        : 'com.squareup.retrofit2:adapter-rxjava2:2.3.0',
                'okhttpInterceptorLogging'     : 'com.squareup.okhttp3:logging-interceptor:3.10.0',
                'rxandroid'                    : 'io.reactivex.rxjava2:rxandroid:2.0.2',
                'rxjava'                       : 'io.reactivex.rxjava2:rxjava:2.1.9',
                'rxrelay'                      : 'com.jakewharton.rxrelay2:rxrelay:2.0.0',
                'gson'                         : 'com.google.code.gson:gson:2.8.2',
                'butterknife'                  : 'com.jakewharton:butterknife:8.6.0',
                'butterknifeAnnotation'        : 'com.jakewharton:butterknife-compiler:8.6.0',
                'glide'                        : 'com.github.bumptech.glide:glide:4.6.1',
                'glideAnnotation'              : 'com.github.bumptech.glide:compiler:4.6.1',
                'BaseRecyclerViewAdapterHelper': 'com.github.CymChad:BaseRecyclerViewAdapterHelper:v1.9.7',
                'bottomNavigationBar'          : 'com.ashokvarma.android:bottom-navigation-bar:2.0.4', // 导航栏
                'eventbus'                     : 'org.greenrobot:eventbus:3.0.0',
                'mp4parser'                    : 'com.googlecode.mp4parser:isoparser:1.1.21',
                'androidPdfViewer'             : 'com.github.barteksc:android-pdf-viewer:3.0.0-beta.5',
                'greendao'                     : 'org.greenrobot:greendao:3.2.2',
                'mta'                          : 'com.qq.mta:mta:3.3.1-release',
                'mid'                          : 'com.tencent.mid:mid:3.73-release',
                'mta_visual'                   : 'com.qq.visual:visual:3.3.1-release',
                'arouter_compiler'             : 'com.alibaba:arouter-compiler:1.1.4',
                'arouter'                      : 'com.alibaba:arouter-api:1.3.1',
        ]

        try {
//            revisionDescription = versions.revisionDescriptionCMD.execute().getText().trim()
            revisionDescription = versions.revisionCommitSHACMD.execute().getText().trim()
        } catch (Exception ignore) {
            revisionDescription = "1"
        }

        // 版本号
        verCode = versions.majorNumber * 1000000 + versions.minorNumber * 10000 + versions.revisionNumber
        // 版本名(大版本.小版本.提交次数.提交记录码)
        verName = versions.majorNumber + "." + versions.minorNumber + "." + versions.revisionNumber + "." + revisionDescription
    }
    
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath deps.android.gradlePlugin
        classpath deps.android.greendaoPlugin
        

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
